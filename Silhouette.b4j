AppType=JavaFX
Build1=Default,b4j.example
File1=apiparamfiller.bjl
File10=LeftSplitPane.bjl
File11=LineEditor.bjl
File12=ListView.bjl
File13=main.bjl
File14=MTSetting.bjl
File15=pretranslate.bjl
File16=progress.bjl
File17=projectSetting.bjl
File18=TopRightSplitPane.bjl
File19=TopSplitPane.bjl
File2=APISetting.bjl
File20=TranslationAssist.bjl
File21=TranslationMemory.bjl
File22=VideoPlayer.bjl
File23=WaveForm.bjl
File3=BottomSplitPane.bjl
File4=Editor.bjl
File5=ExportOptions.bjl
File6=generalSettings.bjl
File7=inputbox.bjl
File8=LangaugePairSelector.bjl
File9=langcodes.txt
FileGroup1=Default Group
FileGroup10=Default Group
FileGroup11=Default Group
FileGroup12=Default Group
FileGroup13=Default Group
FileGroup14=Default Group
FileGroup15=Default Group
FileGroup16=Default Group
FileGroup17=Default Group
FileGroup18=Default Group
FileGroup19=Default Group
FileGroup2=Default Group
FileGroup20=Default Group
FileGroup21=Default Group
FileGroup22=Default Group
FileGroup23=Default Group
FileGroup3=Default Group
FileGroup4=Default Group
FileGroup5=Default Group
FileGroup6=Default Group
FileGroup7=Default Group
FileGroup8=Default Group
FileGroup9=Default Group
Group=Default Group
Library1=abplugin
Library10=jreflection
Library11=jshell
Library12=json
Library13=jxui
Library14=xcustomlistview
Library15=xui views
Library16=jrandomaccessfile
Library2=b4xcollections
Library3=b4xtable
Library4=byteconverter
Library5=encryption
Library6=javaobject
Library7=jcore
Library8=jfx
Library9=jokhttputils2
Module1=APIParamsFiller
Module10=MT
Module11=Preferences
Module12=preTranslateDialog
Module13=progressDialog
Module14=Project
Module15=ProjectSettings
Module16=SrtParser
Module17=Utils
Module18=WaveFormBitmapCreator
Module19=WavRandomAccessFile
Module2=ASR
Module3=DraggableRange
Module4=Exporter
Module5=ExportOptions
Module6=FFMpeg
Module7=InputBox
Module8=jVLC
Module9=LanguagePairSelector
NumberOfFiles=23
NumberOfLibraries=16
NumberOfModules=19
Version=10
@EndOfDesignText@
#Region Project Attributes 
	#MainFormWidth: 600
	#MainFormHeight: 600 
	#VirtualMachineArgs: --add-opens javafx.graphics/com.sun.glass.ui=ALL-UNNAMED
#End Region

#AdditionalJar: jna-5.16.0
#AdditionalJar: slf4j-api-1.7.10
#AdditionalJar: jna-platform-5.16.0
#AdditionalJar: vlcj-4.8.3
#AdditionalJar: vlcj-javafx-1.2.0
#AdditionalJar: vlcj-natives-4.8.3
#AdditionalJar: sqlite-jdbc-3.43.0.0

Sub Process_Globals
	Private fx As JFX
	Private MainForm As Form
	Private xui As XUI 
	Private vlc As jVLC
	Private MenuBar1 As MenuBar
	Private MainPane As Pane
	Private EditorSplitPane As SplitPane
	Private VLCPane As Pane
	Private PlayButton As Button
	Private ProgressSlider As Slider
	Private VolSlider As Slider
	Private ProgressLabel As Label
	Private TotalTimeLabel As Label
	Private ProgressTimer As Timer
	Private ProgressContextMenu As ContextMenu
	Private WaveFormContextMenu As ContextMenu
	Private RightClickTarget As Node
	Private BottomSplitPane As SplitPane
	Private TopSplitPane As SplitPane
	Private TopRightSplitPane As SplitPane
	Private SourceTextArea As TextArea
	Private TargetTextArea As TextArea
	Private LinesTable As B4XTable
	Private currentProject As Project
	Private tableCM As ContextMenu
	Private robot As JavaObject
	Private selectedRowID As Int = -1
	Private expectedEndTime As Int = -1
	Private segmentLength As Int = 30
	Private EndTimeTextField As TextField
	Private StartTimeTextField As TextField
	Private StartColumn As B4XTableColumn
	Private EndColumn As B4XTableColumn
	Private SourceColumn As B4XTableColumn
	Private TargetColumn As B4XTableColumn
	Private focusedTimeTextField As TextField
	Private AssistTabPane As TabPane
	Private TMTableView As TableView
	Private TMTextArea As TextArea
	Public plugin As ABPlugin
	Private isLongClick As Boolean = False
	Private VLCImageView As ImageView
	Private OverviewWaveFormImageView As ImageView
	Private WaveFormImageView As ImageView
	Private waveFormCreator As WaveFormBitmapCreator
	Private overviewRange As DraggableRange
	Private cutDurationMs As Long
	Private cutStartTimeMs As Long
	Private timestampRange As DraggableRange
	Private AudioProgressLabel As Label
	Private EnableWaveFormCheckBox As CheckBox
End Sub

Sub AppStart (Form1 As Form, Args() As String)
	'** IMPORTANT - checking if VLC is installed on the computer should be the first thing to execute in your code
	'If vlc.IsVLCInstalled = False Then
		'Implement your own code to tell user that VLC must be installed.
		'Note: if VLC has been installed in a non-standard directory, VLC might not be found.
	'	fx.Msgbox(MainForm,"VLC must be installed on the computer to run this program.","")
	'	ExitApplication
	'End If

	'** OK, VLC was found. We can proceed.

	MainForm = Form1
	MainForm.RootPane.LoadLayout("main")
	MainForm.WindowWidth = 800
	MainForm.Show
	MainPane.LoadLayout("Editor")
	EditorSplitPane.LoadLayout("TopSplitPane")
	EditorSplitPane.LoadLayout("BottomSplitPane")
	EditorSplitPane.Vertical = True
	TopSplitPane.LoadLayout("VideoPlayer")
	TopSplitPane.LoadLayout("TopRightSplitPane")
	TopSplitPane.DividerPositions = Array As Double(0.8,0.2)
	TopRightSplitPane.LoadLayout("WaveForm")
	TopRightSplitPane.LoadLayout("LineEditor")
	TopRightSplitPane.Vertical = True
	BottomSplitPane.LoadLayout("ListView")
	BottomSplitPane.LoadLayout("TranslationAssist")
	AssistTabPane.LoadLayout("TranslationMemory","TM")
	TMTableView.SetColumnWidth(0,50)
	TMTableView.SetColumnWidth(1,50)
	TMTableView.SetColumnWidth(2,200)
	TMTableView.SetColumnWidth(3,100)
	vlc.Initialize(VLCImageView)
	VLCImageView.PreserveRatio = True
	
	ProgressTimer.Initialize("ProgressTimer",100)
	ProgressTimer.Enabled = False
	tableCM.Initialize("tableCM")
	For Each text As String In Array("Play this line","Delete this line","Prepend a line","Append a line")
		Dim mi As MenuItem
		mi.Initialize(text,"tableMenuItem")
		tableCM.MenuItems.Add(mi)
	Next
	robot = robot.InitializeStatic("com.sun.glass.ui.Application").RunMethodJO("GetApplication",Null).RunMethodJO("createRobot",Null)
	Dim jo As JavaObject = ProgressSlider
	Dim e As Object = jo.CreateEventFromUI("javafx.event.EventHandler", "progress_mousemoved", Null)
	jo.RunMethod("setOnMouseMoved", Array(e))
	Dim e As Object = jo.CreateEventFromUI("javafx.event.EventHandler", "progress_mouseclicked", Null)
	jo.RunMethod("setOnMouseClicked", Array(e))
	Dim e As Object = jo.CreateEventFromUI("javafx.event.EventHandler", "progress_mouseexited", Null)
	jo.RunMethod("setOnMouseExited", Array(e))
	ProgressSlider.tag = False
	ProgressContextMenu.Initialize("ProgressContextMenu")
	WaveFormContextMenu.Initialize("WaveFormContextMenu")
	SetTable
	loadPlugins
	AddWaveFormControls
End Sub

Private Sub AddWaveFormControls
	Dim overviewPane As Pane = overviewRange.Initialize(Me,"overviewRange")
	Dim timestampRangePane As Pane = timestampRange.Initialize(Me,"timestampRange")
	Dim parent As Pane = OverviewWaveFormImageView.Parent
	parent.AddNode(overviewPane,OverviewWaveFormImageView.Left,OverviewWaveFormImageView.Top,OverviewWaveFormImageView.Width,OverviewWaveFormImageView.Height)
	parent.AddNode(timestampRangePane,WaveFormImageView.Left,WaveFormImageView.top,WaveFormImageView.width,WaveFormImageView.Height)
End Sub


'Return true to allow the default exceptions handler to handle the uncaught exception.
Sub Application_Error (Error As Exception, StackTrace As String) As Boolean
    fx.Msgbox(MainForm,Error.Message,"Error")
	Return False
End Sub


Public Sub loadPlugins
	Dim dir As String
	dir=File.Combine(File.DirApp,"plugins")
	plugin.Initialize("plugin",dir, "MyKey")
	plugin.Start(1)
	Log(plugin.GetAvailablePlugins)
End Sub

Sub plugin_PluginsChanged()
	Log("plugins have changed!")
	Log(plugin.GetAvailablePlugins)
	plugin.Stop
End Sub

private Sub SetTable
	LinesTable.HeaderFont = xui.CreateFont(fx.DefaultFont(15),15)
	LinesTable.HeadersHeight = 30dip
	LinesTable.RowHeight = 30dip
	Dim NumberColumn As B4XTableColumn = LinesTable.AddColumn("No.", LinesTable.COLUMN_TYPE_TEXT)
	NumberColumn.Width = 80dip
	NumberColumn.Sortable = False
	StartColumn = LinesTable.AddColumn("Start", LinesTable.COLUMN_TYPE_TEXT)
	StartColumn.Width = 105dip
	StartColumn.Sortable = False
	EndColumn = LinesTable.AddColumn("End", LinesTable.COLUMN_TYPE_TEXT)
	EndColumn.Width = 105dip
	EndColumn.Sortable = False
	SourceColumn = LinesTable.AddColumn("Source", LinesTable.COLUMN_TYPE_TEXT)
	SourceColumn.Sortable = False
	TargetColumn = LinesTable.AddColumn("Target", LinesTable.COLUMN_TYPE_TEXT)
	TargetColumn.Sortable = False
	LinesTable.SearchVisible = False
	LinesTable.SelectionColor = xui.Color_Transparent
End Sub

Private Sub GetTimeLabelText(totalSeconds As Long) As String
	Dim minutes As Int =  Floor(totalSeconds / 60)
	Dim seconds As Int = totalSeconds - minutes*60
	Return NumberFormat(minutes,2,0) & ":" & NumberFormat(seconds,2,0)
End Sub

Private Sub MenuBar1_Action
	Dim mi As MenuItem = Sender
	Select mi.Text
		Case "_Open Media File"
			OpenFile
		Case "_Save"
			Save
		Case "_Close"
			Close
		Case "_Preferences"
			Dim pref As Preferences
			pref.Initialize(MenuBar1)
			pref.Show
		Case "_Recognize Speech"
			RecognizeSpeech
		Case "_Pretranslate"
			Dim dialog As preTranslateDialog
			dialog.Initialize
			Dim options As Map = dialog.ShowAndWait
			pretranslate(options)
		Case "_Settings"
			Dim ps As ProjectSettings
			ps.Initialize(currentProject)
			ps.show
		Case "import from _srt"
			Dim fc As FileChooser
			fc.Initialize
			Dim path As String = fc.ShowOpen(MainForm)
			ImportFromSRT(path)
			LoadLinesToTable
			fx.Msgbox(MainForm,"Done","")
		Case "export to _srt"
			Dim fc As FileChooser
			fc.Initialize
			Dim path As String = fc.ShowSave(MainForm)
			Exporter.ExportToSRT(currentProject.lines,path)
			fx.Msgbox(MainForm,"Done","")
		Case "_Exit"
			ExitApp
		Case "_About"
			fx.Msgbox(MainForm,"@xulihang, 2024","Silhouette")
	End Select
End Sub

Private Sub RecognizeSpeech
	If currentProject.IsInitialized=False Then
		Return
	End If
	
	Dim parent As String = currentProject.GetMediaFolder
	Dim filename As String = currentProject.GetMediaFilename
	Dim dir As String = currentProject.GetTmpFolder

	If File.Exists(dir,"audio.wav") = False Then
		progressDialog.Show("Converting to audio...")
		progressDialog.update2("Processing...")
		Dim outpath As String = File.Combine(dir,"audio.wav")
		Log("convert video to wav...")
		Wait For (FFMpeg.Video2Wav(parent,filename,outpath)) Complete (done As Object)
		Log("convert video to wav done")
		progressDialog.close
	End If
	If File.Exists(dir,"segnemnt-00001.wav") = False Then
		progressDialog.Show("Splitting audio...")
		Log("split wav...")
		Wait For (FFMpeg.SplitWav(segmentLength,dir,"audio.wav")) Complete (done As Object)
		Log("split wav done")
		progressDialog.close
	End If
	Dim files As List = File.ListFiles(dir)
	Dim wavFiles As List
	wavFiles.Initialize
	For Each filename As String In files
		If filename.StartsWith("segment") And filename.EndsWith(".wav") Then
			wavFiles.Add(filename)
		End If
	Next
	progressDialog.Show("Recognizing audio...")
	Dim index As Int = 1
	For Each filename As String In wavFiles
		Log(index & "/" & wavFiles.Size)
		Dim purename As String = Utils.GetFilenameWithoutExtension(filename)
		If File.Exists(dir,purename&".srt") = False Then
			progressDialog.update(index,wavFiles.Size)
			Dim wavpath As String = File.Combine(dir,filename)
			Wait For (ASR.RecognizeWav(wavpath,currentProject.SourceLang)) Complete (done As Object)
		End If
		index = index + 1
	Next
	progressDialog.close
	LoadSRTToProject
	LoadLinesToTable
End Sub

Private Sub LoadSRTToProject
	Dim parser As SrtParser
	parser.Initialize
	Dim dir As String = currentProject.GetTmpFolder
	Dim files As List = File.ListFiles(dir)
	currentProject.Clear
	Dim index As Int = 0
	For Each filename As String In files
		Dim offsetMS As Int = index * segmentLength * 1000
		If filename.EndsWith(".srt") Then
			Dim content As String = File.ReadString(dir,filename)
			content = Utils.RemoveBOM(content)
			Dim lines As List = parser.Parse(content)
			For Each line As SpeechLine In lines
				Dim startTime As String = Utils.ShiftTimeString(line.startTime,offsetMS)
				Dim endTime As String = Utils.ShiftTimeString(line.endTime,offsetMS)
				currentProject.AddLine(startTime,endTime,line.text,"")
			Next
			index = index + 1
		End If
	Next
End Sub

Private Sub ImportFromSRT(path As String)
	Dim parser As SrtParser
	parser.Initialize
	currentProject.Clear
	Dim content As String = File.ReadString(path,"")
	content = Utils.RemoveBOM(content)
	Dim lines As List = parser.Parse(content)
	For Each line As SpeechLine In lines
		Dim startTime As String = line.startTime
		Dim endTime As String = line.endTime
		currentProject.AddLine(startTime,endTime,line.text,"")
	Next
End Sub

Private Sub LoadLinesToTable
	Dim data As List
	data.Initialize
	Dim index As Int = 0
	For Each line As Map In currentProject.lines
		index = index + 1
		Dim row(5) As String
		row(0) = index.As(String)
		row(1) = line.Get("startTime")
		row(2) = line.Get("endTime")
		row(3) = line.Get("source")
		row(4) = line.Get("target")
		data.Add(row)
	Next
	LinesTable.SetData(data)
End Sub

Sub LinesTable_CellClicked (ColumnId As String, RowId As Long)
	SelectRow(RowId,xui.Color_RGB(119,198,255),False)
End Sub

Sub LinesTable_CellLongClicked (ColumnId As String, RowId As Long)
	SelectRow(RowId,xui.Color_RGB(119,198,255),True)
	Dim col As B4XTableColumn = LinesTable.GetColumn(ColumnId)
	Dim i As Int = LinesTable.VisibleRowIds.IndexOf(RowId)
	Dim cell As B4XView = col.CellsLayouts.Get(i)
	Dim jo As JavaObject = tableCM '<--------------------------- global ContextMenu object
	Dim x As Double = robot.RunMethod("getMouseX",Null)' <----- global JavaObject object
	Dim y As Double = robot.RunMethod("getMouseY",Null)
	If Utils.DetectOS = "mac" Then
		Dim m As Map = Utils.GetScreenPosition(cell)
		x = m.Get("x") + cell.Width/2
		y = m.Get("y") + cell.Height/2
	End If
	jo.RunMethod("show", Array(cell, x, y))
End Sub

Sub SelectRow(targetRowId As Int, color As Int,fromLongClick As Boolean)    'rowid-1
	For i = 0 To LinesTable.VisibleRowIds.Size - 1
		Dim RowId As Long = LinesTable.VisibleRowIds.Get(i)
		For Each c As B4XTableColumn In LinesTable.Columns
			Dim pnl As B4XView = c.CellsLayouts.Get(i+1)
			If RowId = targetRowId Then
				pnl.Color=color
			Else
				If i Mod 2 = 0 Then
					pnl.Color = LinesTable.EvenRowColor
				Else
					pnl.Color = LinesTable.OddRowColor
				End If
			End If
		Next
	Next
	selectedRowID = targetRowId
	LoadLineToEditor(fromLongClick)
	DrawWaveForm
	SetTheRangeForTheSelectedRow
End Sub

Private Sub SetTheRangeForTheSelectedRow
	Dim line As Map = currentProject.GetLine(selectedRowID - 1)
	Dim startTime As String = line.get("startTime")
	Dim startTimeMs As Long = Utils.GetMillisecondsFromTimeString(startTime)
	Dim endTime As String = line.get("endTime")
	Dim endTimeMs As Long = Utils.GetMillisecondsFromTimeString(endTime)
	Dim cutStartTimePercent As Double = (startTimeMs - cutStartTimeMs) / cutDurationMs
	Dim cutEndTimePercent As Double = (endTimeMs - cutStartTimeMs) / cutDurationMs
	overviewRange.setProgress(cutStartTimePercent,cutEndTimePercent)
	overviewRange.TriggerRangeChanged
	timestampRange.setProgress(0,1)
End Sub

Private Sub WaveFormPane_Resize (Width As Double, Height As Double)
	timestampRange.Pane.SetSize(Width,Height/4*3)
	overviewRange.Pane.Top = Height/4*3
	overviewRange.Pane.SetSize(Width,Height/4)
	If currentProject.IsInitialized And selectedRowID <> - 1 Then
		DrawWaveForm
	End If
End Sub

Private Sub GetWaveFilesToDraw As List
	Dim waveFilesToDraw As List
	waveFilesToDraw.Initialize
	Dim files As List = File.ListFiles(currentProject.GetTmpFolder)
	Dim wavFiles As List
	wavFiles.Initialize
	For Each filename As String In files
		If filename.StartsWith("segment") And filename.EndsWith(".wav") Then
			wavFiles.Add(filename)
		End If
	Next
	Dim line As Map = currentProject.GetLine(selectedRowID - 1)
	Dim startTime As String = line.get("startTime")
	Dim ms As Long = Utils.GetMillisecondsFromTimeString(startTime)
	Dim wavIndex As Int = ms/1000/30
	Dim hasPrevious As Boolean
	If wavFiles.Size>0 Then
		If wavIndex - 1 >=0 Then
			hasPrevious = True
			waveFilesToDraw.Add(wavFiles.Get(wavIndex - 1))
		End If
		If wavIndex < wavFiles.Size Then
			waveFilesToDraw.Add(wavFiles.Get(wavIndex))
		End If
		If wavIndex + 1 <= wavFiles.Size - 1 Then
			waveFilesToDraw.Add(wavFiles.Get(wavIndex + 1))
		End If
	End If
	If hasPrevious Then
		cutStartTimeMs = (wavIndex-1) * 30 * 1000
	Else
		cutStartTimeMs = wavIndex * 30 * 1000
	End If
	Return waveFilesToDraw
End Sub

Private Sub DrawWaveForm
	Log("DrawWaveForm")
	InitializeWaveFormCreatorIfNeeded
	waveFormCreator.StartIndex = 0
	waveFormCreator.EndIndex = waveFormCreator.SampleLength
	OverviewWaveFormImageView.SetImage(waveFormCreator.Draw(OverviewWaveFormImageView.Width,OverviewWaveFormImageView.Height))
End Sub

Private Sub InitializeWaveFormCreatorIfNeeded
	Dim waveFiles As List = GetWaveFilesToDraw
	If waveFormCreator.IsInitialized Then
		Log("waveform creator initialized")
		Dim needReload As Boolean = False
		If waveFormCreator.Tag Is List Then
			Dim prevousList As List = waveFormCreator.Tag
			If prevousList.Get(0) <> waveFiles.Get(0) Then
				needReload = True
			End If
		End If
		If needReload Then
			Log("reinit waveform creator")
			waveFormCreator.Initialize(LoadDataFromWaveFiles(waveFiles))
			waveFormCreator.Tag = waveFiles
		End If
	Else
		waveFormCreator.Initialize(LoadDataFromWaveFiles(waveFiles))
		waveFormCreator.Tag = waveFiles
	End If
End Sub

Private Sub LoadDataFromWaveFiles(waveFiles As List) As Short()
	Dim totalDuration As Int = 0
	Dim bytesArray As List
	bytesArray.Initialize
	For Each filename As String In waveFiles
		Log(filename)
		Dim wavRaf As WavRandomAccessFile
		wavRaf.Initialize(currentProject.GetTmpFolder,filename)
		totalDuration = totalDuration + wavRaf.Duration_ms
		bytesArray.Add(wavRaf.DataBytes)
	Next
	Dim jointBytes() As Byte = Utils.JoinBytes(bytesArray)
	Dim jointShorts() As Short = Utils.getDataShortsFromBytes(jointBytes)
	cutDurationMs = totalDuration
	Return jointShorts
End Sub

Private Sub overviewRange_RangeChanged(startProgress As Double,endProgress As Double)
	Log("overviewRange_RangeChanged")
	If currentProject.IsInitialized And selectedRowID <> -1 Then
		waveFormCreator.StartIndex = startProgress*waveFormCreator.SampleLength
		waveFormCreator.EndIndex = endProgress*waveFormCreator.SampleLength
		WaveFormImageView.SetImage(waveFormCreator.Draw(WaveFormImageView.Width,WaveFormImageView.Height))
	End If
End Sub

Private Sub timestampRange_RangeChanged(startProgress As Double,endProgress As Double)
	Log("timestampRange_RangeChanged")
	If currentProject.IsInitialized And selectedRowID <> -1 Then
		Dim startTime As Long  = overviewRange.StartProgress * cutDurationMs
		Dim endTime As Long = overviewRange.EndProgress * cutDurationMs
		Dim clipTime As Long = endTime -startTime
		Dim clipStartTime As Long = clipTime * startProgress + startTime
		Dim clipEndTime As Long = clipTime * endProgress + startTime
		StartTimeTextField.Text = Utils.GetTimeStringFromMilliseconds(clipStartTime)
		EndTimeTextField.Text = Utils.GetTimeStringFromMilliseconds(clipEndTime)
	End If
End Sub

Private Sub overviewRange_MouseMoved(mouseMoveProgress As Double)
	If currentProject.IsInitialized And selectedRowID <> -1 Then
		Dim time As Long  = mouseMoveProgress * cutDurationMs + cutStartTimeMs
		AudioProgressLabel.Text = Utils.GetTimeStringFromMilliseconds(time)
	End If
End Sub

Private Sub timestampRange_MouseMoved(mouseMoveProgress As Double)
	If currentProject.IsInitialized And selectedRowID <> -1 Then
		Dim startTime As Long  = overviewRange.StartProgress * cutDurationMs
		Dim endTime As Long = overviewRange.EndProgress * cutDurationMs
		Dim clipTime As Long = endTime - startTime
		Dim clipMouseMoveTime As Long = clipTime * mouseMoveProgress + startTime
		AudioProgressLabel.Text = Utils.GetTimeStringFromMilliseconds(clipMouseMoveTime)
	End If
End Sub

Sub pretranslate(options As Map) As ResumableSub
	If options.Get("type")<>"" Then
		If options.Get("type")="MT" Then
			Dim sourceLang As String
			Dim targetLang As String
			Dim mtengine As String=options.Get("engine")
			sourceLang=currentProject.SourceLang
			targetLang=currentProject.TargetLang
			Dim interval As Int=options.GetDefault("interval",0)
			progressDialog.Show("translating...")
			Dim index As Int = 0
			For Each line As Map In currentProject.lines
				index = index + 1
				progressDialog.update(index,currentProject.lines.Size)
				If progressDialog.isShowing = False Then
					Exit
				End If
				Dim source As String = line.Get("source")
				Dim target As String = line.Get("target")
				If target = "" Then
					wait for (MT.getMT(source,sourceLang,targetLang,mtengine)) complete (target As String)
					line.Put("target",target)
					Sleep(interval)
				End If
			Next
			progressDialog.close
		Else
			fx.Msgbox(MainForm,"Not supported now","")
		End If
	End If
	LoadLinesToTable
	Return ""
End Sub

Private Sub Save
	If currentProject.IsInitialized Then
		currentProject.save
	End If
End Sub

Private Sub Close
	StopPlaying
	LinesTable.SetData(Array())
	Dim currentProject As Project
End Sub

Private Sub OpenFile
	Dim fc As FileChooser
	fc.Initialize
	Dim path As String = fc.ShowOpen(MainForm)
	If File.Exists(path,"") Then
		If currentProject.IsInitialized Then
			Dim currentProject As Project
		End If
		Dim isNew As Boolean = currentProject.Initialize(path)
		If isNew Then
			Dim langSelector As LanguagePairSelector
			langSelector.Initialize
			Dim langpairMap As Map = langSelector.ShowAndWait
			currentProject.SourceLang=langpairMap.Get("source")
			currentProject.TargetLang=langpairMap.Get("target")
		End If
		PlayFile(path)
		LoadLinesToTable
	End If
End Sub

Private Sub PlayFile(path As String)
	If vlc.IsPlaying Then
		vlc.Stop
	End If
	ProgressSlider.tag = False
	ProgressSlider.Value = 0
	vlc.Play(path)
	ProgressSlider.tag = True
	Do While vlc.IsPlaying == False
		Sleep(100)
	Loop
	TogglePlayingStatus(True)
	Dim time As Long = vlc.GetLength / 1000
	Dim minutes As Int =  Floor(time / 60)
	Dim seconds As Int = time - minutes*60
	TotalTimeLabel.Text = NumberFormat(minutes,2,0) & ":" & NumberFormat(seconds,2,0)
End Sub

Sub Progress_MouseExited_Event (MethodName As String, Args() As Object) As Object
	ProgressContextMenu.MenuItems.Clear
	Return Null
End Sub

Sub Progress_MouseClicked_Event (MethodName As String, Args() As Object) As ResumableSub
	If vlc.IsPlayable = False Then
		Return Null
	End If
	If vlc.IsSeekable = False Then
		fx.Msgbox(MainForm,"Not seekable","")
		Return Null
	End If
	Dim e As MouseEvent = Args(0)
	Dim percent As Double = e.X / ProgressSlider.Width
	ProgressSlider.Enabled = False
	ProgressSlider.Value = percent * 100
	vlc.SetPosition(percent)
	ProgressContextMenu.MenuItems.Clear
	e.Consume
	Sleep(0)
	ProgressSlider.Enabled = True
	Return Null
End Sub

Sub Progress_MouseMoved_Event (MethodName As String, Args() As Object) As Object
	If vlc.IsPlayable = False Then
		Return Null
	End If
	Dim e As MouseEvent = Args(0)
	Dim percent As Double = e.X / ProgressSlider.Width
	Dim time As Long = percent * vlc.GetLength / 1000
	ProgressContextMenu.MenuItems.Clear
	Dim mi As MenuItem
	mi.Initialize(GetTimeLabelText(time),"")
	ProgressContextMenu.MenuItems.Add(mi)
	Dim map1 As Map
	map1=Utils.GetScreenPosition(ProgressSlider)
	Dim jo As JavaObject = ProgressContextMenu
	jo.RunMethod("show", Array(ProgressSlider, map1.Get("x")+e.X, map1.Get("y")-40))
	Return Null
End Sub

Private Sub VLCPane_Resize (Width As Double, Height As Double)
	ResizeVLCImageView(Width,Height)
End Sub

Private Sub ResizeVLCImageView(Width As Double, Height As Double)
	Dim img As Image = VLCImageView.GetImage
	If img.IsInitialized Then
		Dim radio As Double = img.Width/img.Height
		VLCImageView.Height = Height
		VLCImageView.Width = Width
		If img.Width/img.Height < Width/Height Then '16:9 4:3
			Dim desiredWidth As Double = VLCImageView.Height * radio
			VLCImageView.Left = (Width - desiredWidth) / 2
		Else
			Dim desiredHeight As Double = VLCImageView.Width / radio
			VLCImageView.Top = (Height - desiredHeight) / 2
		End If
	End If
End Sub

Private Sub ExitApp
	vlc.stop
	vlc.release
	ExitApplication
End Sub

Private Sub VolSlider_ValueChange (Value As Double)
	Log(Value)
	vlc.SetVolume(Value)
End Sub

Private Sub StopButton_MouseClicked (EventData As MouseEvent)
	StopPlaying
End Sub

Private Sub StopPlaying
	vlc.Stop
	TogglePlayingStatus(False)
End Sub

Private Sub PlayButton_MouseClicked (EventData As MouseEvent)
	If vlc.IsPlaying Then
		vlc.Pause
		TogglePlayingStatus(False)
	Else
		vlc.Resume
		Do While vlc.IsPlaying = False
			Sleep(100)
		Loop
		TogglePlayingStatus(True)
	End If
End Sub


Private Sub TogglePlayingStatus(isPlaying As Boolean)
	If isPlaying Then
		vlc.SetVolume(VolSlider.Value)
		VLCPane.PrefWidth  = VLCPane.Width + 1
		VLCPane.PrefWidth  = VLCPane.Width - 1
		PlayButton.Text = ""
		ProgressTimer.Enabled = True
	Else
		PlayButton.Text = ""
		ProgressTimer.Enabled = False
	End If
End Sub

Private Sub ProgressTimer_Tick
	If vlc.IsPlaying Then
		If vlc.GetTime > expectedEndTime And expectedEndTime<>-1 Then
			expectedEndTime = -1
			vlc.Pause
			TogglePlayingStatus(False)
		End If
		ProgressSlider.Tag = False
		DrawPositionLineInWaveForm(vlc.GetTime)
		Dim time As Long = vlc.GetTime / 1000
		Dim minutes As Int = Floor(time / 60)
		Dim seconds As Int = time - minutes*60
		ProgressLabel.Text = NumberFormat(minutes,2,0) & ":" & NumberFormat(seconds,2,0)
		ProgressSlider.Value = vlc.GetTime/vlc.GetLength * 100
		Sleep(0)
		ProgressSlider.Tag = True
	Else
		TogglePlayingStatus(False)
	End If
End Sub

Private Sub DrawPositionLineInWaveForm(time As Long)
	If currentProject.IsInitialized And selectedRowID <> -1 Then
		Dim line As Map = currentProject.GetLine(selectedRowID - 1)
		Dim startTime As String = line.Get("startTime")
		Dim startTimeMs As Long = Utils.GetMillisecondsFromTimeString(startTime)
		If startTimeMs >= cutStartTimeMs And startTimeMs <= cutStartTimeMs + cutDurationMs Then
			overviewRange.PlayTimeProgress = (time - cutStartTimeMs) / cutDurationMs
			Dim clipStartTime As Long  = overviewRange.StartProgress * cutDurationMs
			Dim clipEndTime As Long = overviewRange.EndProgress * cutDurationMs
			Dim clipTime As Long = clipEndTime - clipStartTime
			timestampRange.PlayTimeProgress = (time - cutStartTimeMs - clipStartTime) / clipTime
		End If
	End If
End Sub

Private Sub WaveFormPane_MouseClicked (EventData As MouseEvent)
	Dim view As Node = Sender
	Dim jo As JavaObject = EventData
	If EventData.SecondaryButtonPressed Then
		RightClickTarget = jo.RunMethod("getTarget",Null)
		Dim isTimestampRange As Boolean = False
		If RightClickTarget Is ImageView Then
			Dim iv As ImageView = RightClickTarget
			isTimestampRange = (iv == timestampRange.ImageView)
		End If
		SetWaveFormContextMenuItems(isTimestampRange)
		Dim map1 As Map
		map1=Utils.GetScreenPosition(view)
		Dim jo As JavaObject = WaveFormContextMenu
		jo.RunMethod("show", Array(view, map1.Get("x")+EventData.X, map1.Get("y")+EventData.Y))
	End If
End Sub

Private Sub SetWaveFormContextMenuItems(isTimestampRange As Boolean)
	WaveFormContextMenu.MenuItems.Clear
	If isTimestampRange Then
		Dim playMi As MenuItem
		playMi.Initialize("Play the selection","playSelectionMI")
		WaveFormContextMenu.MenuItems.Add(playMi)
	End If
	Dim playHereMi As MenuItem
	playHereMi.Initialize("Play from here","playHereMI")
	WaveFormContextMenu.MenuItems.Add(playHereMi)
End Sub

Private Sub playHereMI_Action
	Dim time As Long = Utils.GetMillisecondsFromTimeString(AudioProgressLabel.Text)
	Log(time)
	vlc.SetTime(time)
	If vlc.IsPlaying = False Then
		vlc.Resume
		TogglePlayingStatus(True)
	End If
End Sub

Private Sub playSelectionMI_Action
	
End Sub


Sub MainForm_CloseRequest (EventData As Event)
	Log("We are closing the mainform")
	vlc.stop
	vlc.release
	Sleep(0)
	ExitApplication
End Sub

Private Sub tableMenuItem_Action
	Dim mi As MenuItem = Sender
	Select mi.Text
	    Case "Play this line"
			PlayTheSelectedLine
		Case "Prepend a line"
			PrependLine
		Case "Append a line"
			AppendLine
		Case "Delete this line"
			currentProject.DeleteLine(selectedRowID - 1)
			LoadLinesToTable
	End Select
End Sub

Private Sub PrependLine
	currentProject.PrependLine(selectedRowID - 1)
	LoadLinesToTable
End Sub

Private Sub AppendLine
	currentProject.AppendLine(selectedRowID - 1)
	LoadLinesToTable
End Sub

Private Sub PlayTheSelectedLine
	Dim line As Map = currentProject.GetLine(selectedRowID - 1)
	Dim startTime As Int = Utils.GetMillisecondsFromTimeString(line.Get("startTime"))
	expectedEndTime = Utils.GetMillisecondsFromTimeString(line.Get("endTime"))
	vlc.Resume
	vlc.SetTime(startTime)
	TogglePlayingStatus(True)
End Sub

Private Sub LoadLineToEditor(fromLongClick As Boolean)
	isLongClick = fromLongClick
	Dim line As Map = currentProject.GetLine(selectedRowID - 1)
	SourceTextArea.Text = line.Get("source")
	TargetTextArea.Text = line.Get("target")
	StartTimeTextField.Text = line.Get("startTime")
	EndTimeTextField.Text = line.Get("endTime")
	If fromLongClick = False Then
		clearTMandMT
		ShowMT(SourceTextArea.Text,True)
	End If
	Sleep(200)
	isLongClick = False
End Sub

Private Sub clearTMandMT
	TMTableView.Items.Clear
	TMTextArea.Text=""
End Sub

Private Sub ShowMT(sourceText As String,fromTextArea As Boolean)
	Dim preferencesMap As Map = Utils.getPrefMap
	If preferencesMap.ContainsKey("mt") Then
		Dim mtPreferences As Map=preferencesMap.Get("mt")
		For Each engine As String In MT.getMTList
			If mtPreferences.GetDefault(engine&"_enabled",False)=True Then
				appendSingleMTResult(sourceText,fromTextArea,engine)
			End If
		Next
	End If
End Sub

Private Sub appendSingleMTResult(sourceText As String,fromTextArea As Boolean,engine As String)
	wait for (MT.getMT(sourceText,currentProject.SourceLang,currentProject.TargetLang,engine)) Complete (target As String)
	If fromTextArea And sourceText <> SourceTextArea.Text Then
		Return
	End If
	Dim lbl As Label
	lbl.Initialize("lbl")
	lbl.Text=target
	lbl.TooltipText=target
	Dim row() As Object
	row=Array("","",lbl,engine)
	Log(target)
	TMTableView.Items.Add(row)
End Sub


Sub tmTableView_MouseClicked (EventData As MouseEvent)
	If TMTableView.SelectedRow=-1 Then
		Return
	End If
	If TMTableView.SelectedRowValues=Null Then
		Return
	End If
	Dim lbl As Label=TMTableView.SelectedRowValues(2)
	If EventData.ClickCount=2 Then
		TargetTextArea.Text=lbl.Text
	Else if EventData.ClickCount=1 Then
		TMTextArea.Text=lbl.Text
	End If
End Sub

Private Sub UpdateTime(isStartTime As Boolean,new As String)
	If currentProject <> Null And currentProject.IsInitialized = False Then
		Return
	End If
	
	If isLongClick Then
		Return
	End If
	
	Dim line As Map = currentProject.GetLine(selectedRowID - 1)
	If isStartTime Then
		line.Put("startTime",new)
		LinesTable.sql1.ExecNonQuery2($"UPDATE data SET ${StartColumn.SQLID} = ? WHERE rowid = ?"$, Array As String(new, selectedRowID))
	Else
		line.Put("endTime",new)
		LinesTable.sql1.ExecNonQuery2($"UPDATE data SET ${EndColumn.SQLID} = ? WHERE rowid = ?"$, Array As String(new, selectedRowID))
	End If
	LinesTable.Refresh
End Sub

Private Sub UpdateText(isSource As Boolean,new As String)
	If currentProject <> Null And currentProject.IsInitialized = False Then
		Return
	End If
	If isLongClick Then
		Return
	End If
	
	Dim line As Map = currentProject.GetLine(selectedRowID - 1)
	If isSource Then
		line.Put("source",new)
		LinesTable.sql1.ExecNonQuery2($"UPDATE data SET ${SourceColumn.SQLID} = ? WHERE rowid = ?"$, Array As String(new, selectedRowID))
	Else
		line.Put("target",new)
		LinesTable.sql1.ExecNonQuery2($"UPDATE data SET ${TargetColumn.SQLID} = ? WHERE rowid = ?"$, Array As String(new, selectedRowID))
	End If
	LinesTable.Refresh
End Sub

Private Sub SourceTextArea_TextChanged (Old As String, New As String)
	UpdateText(True,New)
End Sub

Private Sub TargetTextArea_TextChanged (Old As String, New As String)
	UpdateText(False,New)
End Sub

Private Sub StartTimeTextField_TextChanged (Old As String, New As String)
	UpdateTime(True,New)
End Sub

Private Sub EndTimeTextField_TextChanged (Old As String, New As String)
	UpdateTime(False,New)
End Sub

Private Sub UseCurrentTimeButton_MouseClicked (EventData As MouseEvent)
	If focusedTimeTextField.IsInitialized Then
		focusedTimeTextField.Text = Utils.GetTimeStringFromMilliseconds(vlc.GetTime)
	End If
End Sub

Private Sub StartTimeTextField_FocusChanged (HasFocus As Boolean)
	If HasFocus Then
		focusedTimeTextField = StartTimeTextField
	End If
End Sub

Private Sub EndTimeTextField_FocusChanged (HasFocus As Boolean)
	If HasFocus Then
		focusedTimeTextField = EndTimeTextField
	End If
End Sub
